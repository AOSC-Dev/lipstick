#!/usr/bin/awk -f
BEGIN       { root = "/tmp/duihome_benchmarks/"
              files[1] = root "benchmarkresult.txt"
              files[2] = root "duihome_benchmark_commands.txt"
              pad = step = ""
            }

/<get>/     ||
/<\/get>/   ||
/<file>/    { next }

/<\/set>/   { print pad "<get>"
              for (i in files)
                print pad step "<file>" files[i] "</file>"
              print pad "</get>"
            }

/^[ ]+</    { s = $0;
              sub(/[^ ].*/, "", s)
              if (length(s) > length(pad))
                step = substr(s, length(pad) + 1)
              pad = s
            }

//          { print }
